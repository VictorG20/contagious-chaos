{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "92e9122d",
   "metadata": {},
   "source": [
    "# Compartmental models for disease spread: Endemic state\n",
    "\n",
    "\n",
    "## Instructions:\n",
    "\n",
    "* Run the cell [Packages and Functions](#Packages-and-Functions) first, all the relevant code is there (except, maybe, some plotting routines).\n",
    "\n",
    "\n",
    "## Notation:\n",
    "\n",
    "* $S$ and $I$ denote *fractions* of susceptible and infected population.\n",
    "* $t$ is in units of $1/\\gamma$, the inverse of the recovery rate.\n",
    "* $R_{0}$ represents the basic reproduction number/rate.\n",
    "* $\\nu$ is the ratio between waining immunity rate and recovery rate.\n",
    "\n",
    "\n",
    "## Index\n",
    "\n",
    "### [1. SIR and SIRS models - Relevant formulae](#1-SIR-and-SIRS-models---Relevant-Formulae)\n",
    "\n",
    "### [2. Comparison of SIR and SIRS models](#2-Comparison-of-SIR-and-SIRS-models)\n",
    "\n",
    "### [3. Bifurcation diagram](#3-Bifurcation-Diagram)\n",
    "\n",
    "### [Packages and functions](#Packages-and-Functions)\n",
    "\n",
    "<hr style=\"border: 1px solid black; width:100%;\"></hr>\n",
    "\n",
    "\n",
    "## 1 SIR and SIRS models - Relevant Formulae\n",
    "\n",
    "Since for both cases the total population is conserved in time, $\\dot{S} + \\dot{I} + \\dot{R} = 0$, we can treat the system in terms of two variables instead of three through the relation $R = 1 - S - I$. Then, the systems read\n",
    "\n",
    "\\begin{equation}\n",
    "    \\text{SIR model:} \\quad\n",
    "    \\left\\{\n",
    "    \\begin{aligned}\n",
    "        \\frac{\\mathrm{d}S}{\\mathrm{d}t} & = -R_{0} S I \\\\\n",
    "        \\frac{\\mathrm{d}I}{\\mathrm{d}t} & = (R_{0} S - 1) I \n",
    "    \\end{aligned}\n",
    "    \\right\\} \\qquad \\qquad \n",
    "    \\text{SIRS model:} \\quad\n",
    "    \\left\\{\n",
    "    \\begin{aligned}\n",
    "        \\frac{\\mathrm{d}S}{\\mathrm{d}t} & =  -R_{0} S I + \\nu(1 - S - I) \\\\\n",
    "        \\frac{\\mathrm{d}I}{\\mathrm{d}t} & = (R_{0} S - 1) I \n",
    "    \\end{aligned}\n",
    "    \\right\\}\n",
    "\\end{equation}\n",
    "\n",
    "For the SIR system we always have a **single stable fixed point** given by\n",
    "\n",
    "\\begin{equation}\n",
    "    S^{*} = I_{0} + S_{0} + \\frac{1}{R_{0}} \\ln{\\left(\\frac{S^{*}}{S_{0}}\\right)}, \\qquad I^{*} = 0.\n",
    "\\end{equation}\n",
    "\n",
    "For the SIRS model we have two fixed points\n",
    "\n",
    "\\begin{equation}\n",
    "    P_{1} : S^{*}_{1} = 1, \\quad I^{*}_{1} = 0, \\qquad \\qquad P_{2}: S^{*}_{2} = \\frac{1}{R_{0}}, \\quad I^{*}_{2} = \\frac{\\nu(R_{0} - 1)}{R_{0}(\\nu - 1)}\n",
    "\\end{equation}\n",
    "\n",
    "and their stability depends on the value of $R_{0}$\n",
    "\n",
    "* If $R_{0} < 1$ the **first fixed point is stable and the second one is a saddle node**. However, since the second one lies beyond physical initial conditions ($S(t) \\leq 1 \\; \\forall t$), it is never realized.\n",
    "\n",
    "\n",
    "* If $R_{0} = 1$ both fixed points “merge” into **one stable point**, corresponding to the expression for the first one.\n",
    "\n",
    "\n",
    "* If $R_{0} > 1$ the first one becomes a saddle node, only approached if $I_{0} = 0$, while **the second fixed point is stable**.\n",
    "\n",
    "\n",
    "## 2 Comparison of SIR and SIRS models\n",
    "\n",
    "### [Back to index](#Index)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "55e0ec44",
   "metadata": {},
   "outputs": [],
   "source": [
    "#################################\n",
    "# Initial values and parameters #\n",
    "#################################\n",
    "\n",
    "ν = 0.3  # Ratio: waning immunity / recovery\n",
    "\n",
    "s0 = 0.9\n",
    "i0 = 1 - s0\n",
    "\n",
    "tf = 25.  # Final time for integration.\n",
    "\n",
    "s_curves = []\n",
    "i_curves = []\n",
    "times = []\n",
    "R0_values = []\n",
    "\n",
    "for case in [1, 4, 5]\n",
    "\n",
    "    R0 = get_R0_option(case, ν)\n",
    "    \n",
    "    push!(R0_values, R0)\n",
    "\n",
    "    sir_sol, sirs_sol = get_solutions(s0, i0, R0, ν; tf=tf)\n",
    "    \n",
    "    push!(s_curves, sir_sol[1, :])\n",
    "    push!(s_curves, sirs_sol[1, :])\n",
    "    \n",
    "    push!(i_curves, sir_sol[2, :])\n",
    "    push!(i_curves, sirs_sol[2, :])\n",
    "    \n",
    "    (case == 1) && (times = sir_sol.t)\n",
    "    \n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a3914f00",
   "metadata": {},
   "outputs": [],
   "source": [
    "line_colors = [1 1 :forestgreen :forestgreen 2 2]\n",
    "\n",
    "labels = [\"\\$ $(round(R0_values[1], digits=2))\\$\", false, \n",
    "    \"\\$ $(round(R0_values[2], digits=2))\\$\", false, \n",
    "    \"\\$ $(round(R0_values[3], digits=2))\\$\", false]\n",
    "labels = reduce(hcat, labels)\n",
    "\n",
    "# General single-plot attributes\n",
    "kw =(;\n",
    "    xlabel=\"\\$ t \\\\; (1/\\\\gamma \\\\; \\\\mathrm{units}) \\$\",\n",
    "    xminorgrid=true,\n",
    "    yminorgrid=true,\n",
    "    yminorticks=5,\n",
    "    xlabelfontsize=15,\n",
    "    ylabelfontsize=15,  # Default value is 11.\n",
    "    legendfontsize=14,\n",
    "    legendtitlefontsize=15,\n",
    "    tickfontsize=10,\n",
    "    framestyle=:box,\n",
    "    dpi=400,\n",
    "    size=(450, 450)\n",
    ")\n",
    "\n",
    "plot1 = plot(times, s_curves, label=false, lc=line_colors, ls=[:solid :dash], lw=2, \n",
    "        ylabel=\"\\$ S \\$\"; kw...)\n",
    "\n",
    "plot2 = plot(times, i_curves, label=labels, lc=line_colors, ls=[:solid :dash], lw=2, \n",
    "        ylabel=\"\\$ I \\$\", legendtitle=\"\\$ R_{0} \\$\", legend=:outertopright; kw...)\n",
    "\n",
    "my_plot = plot(plot1, plot2, layout=grid(1, 2, widths=(4.45/10,5.55/10)), size=(910, 450), \n",
    "        left_margin=5Plots.mm, bottom_margin=5Plots.mm)\n",
    "\n",
    "# savefig(my_plot, \"Plots/prob1_b_model_comparison.png\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4463c762",
   "metadata": {},
   "source": [
    "## 3 Bifurcation Diagram\n",
    "\n",
    "### [Back to index](#Index)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "aabaa461",
   "metadata": {},
   "outputs": [],
   "source": [
    "ν = 0.4  # Ratio: waning immunity / recovery\n",
    "\n",
    "R0_min = 0.1\n",
    "R0_max = 3\n",
    "\n",
    "R0_range1 = R0_min:0.01:1\n",
    "R0_range2 = 1:0.01:R0_max\n",
    "\n",
    "# Second fixed point value for I\n",
    "i_star2(R0, ν) = ν*(R0 - 1)/(R0*(1 + ν))\n",
    "\n",
    "\n",
    "# General plot attributes\n",
    "kw =(;\n",
    "    xlabel=\"\\$ R_{0} \\$\",\n",
    "    ylabel=\"\\$ I^{*} \\$\",\n",
    "    xrange=(0, R0_max+0.02),\n",
    "    yrange=(-0.1, i_star2(R0_max, ν)),\n",
    "    xminorgrid=true,\n",
    "    yminorgrid=true,\n",
    "    xminorticks=5,\n",
    "    yminorticks=5,\n",
    "    xlabelfontsize=15,\n",
    "    ylabelfontsize=15,  # Default value is 11.\n",
    "    legendfontsize=14,\n",
    "    legendtitlefontsize=15,\n",
    "    tickfontsize=10,\n",
    "    framestyle=:box,\n",
    "    dpi=400,\n",
    "    size=(450, 450)\n",
    ")\n",
    "\n",
    "bif_diag = plot(R0_range1, i_star2.(R0_range1, ν), ls=:dash, lc=1, lw=2, label=false; kw...)\n",
    "plot!(R0_range2, i_star2.(R0_range2, ν), ls=:solid, lc=1, lw=2, label=false)\n",
    "plot!(R0_range1, zeros(length(R0_range1)), ls=:solid, lc=2, lw=2, label=false)\n",
    "plot!(R0_range2, zeros(length(R0_range2)), ls=:dash, lc=2, lw=2, label=false)\n",
    "\n",
    "# savefig(bif_diag, \"Plots/prob1_b_bif_diagram.png\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a42809d1",
   "metadata": {},
   "source": [
    "## Packages and Functions\n",
    "\n",
    "### [Back to index](#Index)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fa2b62ee",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Roots\n",
    "using DifferentialEquations\n",
    "using Plots\n",
    "\n",
    "\n",
    "###################################\n",
    "# Definition of SIR & SIRS models #\n",
    "###################################\n",
    "\n",
    "function sir!(du, u, R0, t)\n",
    "    s, i = u\n",
    "    du[1] = ds = -R0 * s * i\n",
    "    du[2] = di = R0 * s * i - i\n",
    "end\n",
    "\n",
    "\n",
    "function sirs!(du, u, params, t)\n",
    "    R0, ν = params \n",
    "    s, i = u\n",
    "    du[1] = ds = -R0*s*i + ν*(1 - s - i)\n",
    "    du[2] = di = (R0*s - 1.)*i\n",
    "end\n",
    "\n",
    "\n",
    "########################################\n",
    "# Wrapper for computing both solutions #\n",
    "########################################\n",
    "\n",
    "function get_solutions(s0, i0, R0, ν; t0=0, tf=50., save_steps=0.01)\n",
    "   \n",
    "    # Declare ODE Problems\n",
    "    u0 = [s0, i0]\n",
    "    params = [R0, ν]\n",
    "    tspan = (t0, tf)\n",
    "    sir_prob = ODEProblem(sir!, u0, tspan, R0)\n",
    "    sirs_prob = ODEProblem(sirs!, u0, tspan, params)\n",
    "    \n",
    "    # Solve\n",
    "    sir_sol = solve(sir_prob, saveat = save_steps)\n",
    "    sirs_sol = solve(sirs_prob, saveat = save_steps)\n",
    "\n",
    "    return sir_sol, sirs_sol\n",
    "end\n",
    "\n",
    "\n",
    "# Trace and discriminant of the Jacobian matrix at \n",
    "# the fixed point S = 1/R0, I = ν(R0 - 1)/R0(1+ν).\n",
    "τ(R0, ν) = -ν*(R0 + ν)/(1 + ν)\n",
    "Δ(R0, ν) = round((τ(R0, ν))^2 - 4*ν*(R0 - 1), digits=13)\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    get_R0_option(num, ν)\n",
    "\n",
    "Get an R0 value representative of one of the possible\n",
    "cases for the stability of the fixed points.\n",
    "\n",
    "1. First f.p. is a stable node, second is a saddle node.\n",
    "2. First f.p. is a stable degenerate node, second is a saddle node.\n",
    "3. Single stable node.\n",
    "4. First f.p. is a saddle node, second is a stable node.\n",
    "5. First f.p. is a saddle node, second is a stable spiral.\n",
    "6. Degenerate stable node.\n",
    "\"\"\"\n",
    "function get_R0_option(num, ν)\n",
    "    if num == 1\n",
    "        R0 = (0.7 != 1-ν) ? 0.7 : 0.8\n",
    "        \n",
    "    elseif num == 2\n",
    "        R0 = 1 - ν\n",
    "        \n",
    "    elseif num == 3\n",
    "        R0 = 1\n",
    "    \n",
    "    elseif num == 4\n",
    "        R0 = ((ν^2 + 4*ν + 2 - 2*(1+ν)^(3/2))/ν + 1)/2\n",
    "\n",
    "    elseif num == 5\n",
    "        R0_min = (ν^2 + 4*ν + 2 - 2*(1+ν)^(3/2))/ν\n",
    "        R0_max = (ν^2 + 4*ν + 2 + 2*(1+ν)^(3/2))/ν\n",
    "\n",
    "        R0_values = range(R0_min, step=(R0_max - R0_min)/100, length=100)\n",
    "\n",
    "        _, idx = findmax(sqrt.(-Δ.(R0_values, ν)) ./ (-τ.(R0_values, ν)))\n",
    "\n",
    "        R0 = R0_values[idx]\n",
    "\n",
    "    elseif num == 6\n",
    "        R0 = (ν^2 + 4*ν + 2 - 2*(1+ν)^(3/2))/ν\n",
    "        \n",
    "    else\n",
    "        error(\"Fo' shizzle, dog\")\n",
    "    end\n",
    "    \n",
    "    println(\"R0 value of $(round(R0, digits=3))\")\n",
    "\n",
    "    return R0\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": null,
   "lastKernelId": null
  },
  "kernelspec": {
   "display_name": "Julia 1.8.5",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
